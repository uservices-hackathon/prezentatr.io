buildscript {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.2.3.RELEASE"
        classpath 'com.ofg:uptodate-gradle-plugin:1.5.1'
        classpath 'se.transmode.gradle:gradle-docker:1.2'
        classpath "io.codearte.accurest:accurest-gradle-plugin:$accurestVersion"
        classpath 'com.blogspot.toomuchcoding:gradle-test-profiler:0.3.1'
        classpath 'org.ajoberstar:gradle-git:0.12.0'

        if (project.hasProperty("coverage")) {
            classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:1.0.2'
        }
    }
}

apply plugin: 'groovy'
apply plugin: 'spring-boot'
apply plugin: 'maven-publish'
apply plugin: 'com.ofg.uptodate'
apply plugin: 'docker'
apply plugin: 'accurest'
apply plugin: 'com.blogspot.toomuchcoding.testprofiler'

ext {
    buildNrLoc = project.hasProperty('buildNr') ? "${buildNr}" : "CD-000"
    projectGroupId = groupId

    contractsDir = file("${project.rootDir}/repository/mappings/")
    wiremockStubsOutputDirRoot = file("${project.buildDir}/production/${project.name}-stubs/")
    wiremockStubsOutputDir = new File(wiremockStubsOutputDirRoot, 'mappings/')
}

group = projectGroupId
version = buildNrLoc

sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDirs += ["src/main/java"]

sourceCompatibility = '1.8'

configurations {
    all {
        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                // To prevent an accidental usage of groovy-all.jar and groovy.jar in different versions
                // all modularized Groovy jars are replaced with groovy-all.jar by default.
                if (details.requested.group == 'org.codehaus.groovy' && details.requested.name != "groovy-all") {
                    details.useTarget("org.codehaus.groovy:groovy-all:${details.requested.version}")
                }
                if (details.requested.name == 'servlet-api') {
                    details.useTarget("org.apache.tomcat.embed:tomcat-embed-core:8.0.23")
                }
            }
        }
    }
}

project.gradle.projectsEvaluated {
    applicationDefaultJvmArgs = project.gradle.startParameter.systemPropertiesArgs.entrySet().collect{"-D${it.key}=${it.value}"}
}

test {
    jvmArgs = project.gradle.startParameter.systemPropertiesArgs.entrySet().collect{"-D${it.key}=${it.value}"} + ['-Xmx512m']
    testLogging {
        exceptionFormat = 'full'
    }
    exclude '**/*UISpec.*'
}

task guiTest(type: Test) {
    jvmArgs project.gradle.startParameter.systemPropertiesArgs.entrySet().collect{"-D${it.key}=${it.value}"}
    testLogging {
        exceptionFormat = 'full'
    }
    include '**/*UISpec.*'

    reports.html {
        destination = file("${reporting.baseDir}/guiTests")
    }
    group = "Verification"
    description = "Runs the acceptance GUI tests with Geb."
}

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework:spring-context-support"
    compile "org.codehaus.groovy:groovy-all"
    compile 'com.fasterxml.jackson.core:jackson-databind'
    compile "org.codehaus.jackson:jackson-mapper-asl:$jacksonMapper"
    compile "org.codehaus.jackson:jackson-core-asl:$jacksonMapper"
    compile 'com.google.guava:guava:18.0'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.apache.tomcat.embed:tomcat-embed-el'
    compile 'com.jayway.jsonpath:json-path-assert:0.9.1'
    compile 'org.yaml:snakeyaml'
    compile 'org.hibernate:hibernate-validator'
    compile "org.aspectj:aspectjrt"
    compile 'org.codehaus.gpars:gpars:1.2.1'
    compile 'net.sf.ehcache:ehcache:2.10.0'
    compile "com.ofg:micro-infra-spring-boot-starter:$microInfraSpringVersion"
    compile "de.codecentric:spring-boot-admin-starter-client:1.2.3"
    compile 'org.jolokia:jolokia-core'

    runtime 'cglib:cglib-nodep:3.1'
    runtime 'org.objenesis:objenesis:2.1'
    runtime 'org.aspectj:aspectjweaver'
    runtime 'com.h2database:h2'

    testCompile 'com.jayway.awaitility:awaitility:1.6.3'
    compile('com.github.tomakehurst:wiremock:1.55') {
        exclude group: 'org.mortbay.jetty', module: 'servlet-api'
    }
    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "com.ofg:micro-infra-spring-test:$microInfraSpringVersion"

    testCompile "org.gebish:geb-core:$gebVersion"
    testCompile "org.gebish:geb-spock:$gebVersion"
    testCompile "org.spockframework:spock-spring"
    testCompile "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"
    testCompile "org.seleniumhq.selenium:selenium-support:$seleniumVersion"
    testCompile "org.seleniumhq.selenium:selenium-htmlunit-driver:$seleniumVersion"
    testCompile "com.jayway.restassured:rest-assured:$restAssuredVersion"
    testCompile "com.jayway.restassured:spring-mock-mvc:$restAssuredVersion"
    testCompile "io.codearte.accurest:accurest-core:$accurestVersion"
}

task docker(type: Docker) {
    dependsOn 'build'
    dryRun true
    setEnvironment('CONFIG_FOLDER', '/properties')
    addFile("properties", "/properties")
    addFile("${libsDir}/${project.name}-${buildNrLoc}.jar", "/${project.name}/${project.name}.jar")
    exposePort(8095)
    entryPoint(['java', '-jar', "/${project.name}/${project.name}.jar"])
}

testprofiler {
    minTestThreshold = 100

    buildBreaker {
        maxTestThreshold = project.hasProperty('maxTestThreshold') ? project.property('maxTestThreshold') : 30_000
        ifTestsExceedMaxThreshold {
            displayWarning()
        }
    }
}

if (project.hasProperty("coverage")) {
    apply plugin: 'codenarc'
    apply plugin: 'jacoco'
    apply plugin: 'sonar-runner'
    apply plugin: 'com.github.kt3k.coveralls'

    jacoco {
        toolVersion = '0.7.4.201502262128'
    }

    jacocoTestReport {
        reports {
            xml.enabled = true // coveralls plugin depends on xml format report
            html.enabled = true
        }
    }

    codenarc {
        configFile = file('config/codenarc/rules.groovy')
        maxPriority1Violations = 0
        maxPriority2Violations = 6
        maxPriority3Violations = 10
        reportFormat = 'xml'
        ignoreFailures = true
    }

    dependencies {
        codenarc 'org.codenarc:CodeNarc:0.21'
    }

    test {
        jacoco {
            excludes = ['*Configuration']
        }
    }

    sonarRunner {
        sonarProperties {
            //TODO: fill this
            property "sonar.host.url", ""
            property "sonar.junit.reportsPath", "build/test-results"
        }
    }
}

accurest {
    baseClassForTests = 'pl.devoxx.prezentatr.present.BaseMvcSpec'
    basePackageForTests = 'accurest'
    contractsDslDir = contractsDir
    generatedTestSourcesDir = file("${project.buildDir}/generated-sources/accurest")
    stubsOutputDir = wiremockStubsOutputDir
}

test.dependsOn { generateAccurest } //See: https://github.com/Codearte/accurest/issues/36

task stubsJar(type: Jar, dependsOn: ["generateWiremockClientStubs", "addHashFile"]) {
    baseName = "${project.name}-stubs"
    from wiremockStubsOutputDirRoot
    from "commitHash.txt"
}

//Make sure that fat jar is built before uploading
project.tasks.findAll { it.name.startsWith('publish') || it.name == 'bintrayUpload' }*.dependsOn { bootRepackage }

artifacts {
    archives stubsJar
}

publishing {
    repositories {
        maven {
            url getProp('mavenRepoUrl')
            credentials {
                username getProp('mavenUser')
                password getProp('mavenPassword')
            }
        }
    }
    publications {
        stubs(MavenPublication) {
            artifactId "${project.name}-stubs"
            artifact stubsJar
        }
        mavenJava(MavenPublication) {
            from components.java
            pom.withXml {
                //Remove dependencies - everything is embedded in JAR created by Spring Boot
                asNode().dependencies.replaceNode{ null }
            }
        }
    }
}

String getProp(String propName) {
    return hasProperty(propName) ? (getProperty(propName) ?: System.properties[propName]) : System.properties[propName]
}

wrapper {
    gradleVersion '2.2.1'
}

task addHashFile << {
    ext.repo = org.ajoberstar.grgit.Grgit.open(project.file('.'))
    def headCommit = repo.head()
    ext.gitBranch = repo.branch.current.name
    ext.gitCommitId = headCommit.abbreviatedId
    ext.gitCommitTime = new Date(headCommit.time * 1000L).format("yyyy-MM-dd'T'HH:mm:ssZ")

    File resourcesMainDir = new File(project.buildDir, 'resources/main')
    resourcesMainDir.mkdirs()
    File gitPropertiesFile = new File(resourcesMainDir, 'git.properties')
    gitPropertiesFile.text = """git.branch=${gitBranch}
git.commit.id=${gitCommitId}
git.commit.time=${gitCommitTime}
"""
}

jar {
    dependsOn addHashFile
    from "git.properties"
}

bootRepackage {
    withJarTask = "jar"
}